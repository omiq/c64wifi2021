1 POKE 53272,23
2 PRINT CHR$(5): PRINT CHR$(147): PRINT CHR$(19);: REM clear screen, white text
3 OPEN2,2,4,CHR$(8)+CHR$(0):REM OPEN THE SERIAL FROM USER PORT
4 PRINT "STARTING"
5 PRINT#2,"AT":GOSUB 22
6 FOR I = 1 TO 5
7 GOSUB 30
8 NEXT
9 GOSUB 57
10 U$="": I$="": GET I$
11 IF I$="1" THEN PRINT "1 SELECTED": U$="ATGETHTTP://192.168.0.103/win&A=64&PL=1": GOSUB 22
12 IF I$="2" THEN PRINT "2 SELECTED": U$="ATGETHTTP://192.168.0.103/win&A=64&PL=2": GOSUB 22
13 IF I$="3" THEN PRINT "3 SELECTED": U$="ATGETHTTP://192.168.0.103/win&A=64&PL=3": GOSUB 22
14 IF I$="4" THEN PRINT "4 SELECTED": U$="ATGETHTTPS://HOOK.EU2.MAKE.COM/ad3mie2ehs5uuhjo3n630yut9guhiwj1?MESSAGE=C64+BASIC": GOSUB 22
15 IF I$="5" THEN PRINT "5 SELECTED": U$="ATGEThttps://timeapi.io/api/timezone/zone?timezone=europe/london": GOSUB 35
16 IF I$ = "" THEN GOTO 10: REM LOOP BACK TO START OF THE MAIN LOOP AGAIN
17 FOR I = 1 TO 5
18 GOSUB 30
19 NEXT
20 GOTO 9
21 CLOSE 2: END
22 PRINT#2,U$ + CHR$(10)+CHR$(13);:U$=""
23 PRINT ""
24 IF PEEK(673) AND 1 THEN PRINT ".";: GOTO 24: REM STILL TRANSMITTING
25 FOR I = 1 TO 5
26 GOSUB 30
27 NEXT
28 RETURN
29 REM GET THE RESPONSE UNTIL NOTHING WAITING
30 GET#2,S$:IF S$<>"" THEN PRINT S$;:GOTO 30
31 FOR W=1 TO 2000: NEXT
32 RETURN
33 REM GET THE URL AND THEN
34 REM PARSE THE RESULT FROM TIMEAPI
35 PRINT#2,U$ + CHR$(10)+CHR$(13);:U$=""
36 PRINT ""
37 IF PEEK(673) AND 1 THEN PRINT ".";: GOTO 37: REM STILL TRANSMITTING
38 REM GET THE TIME
39 CC=0 : REM THE API WILL RETURN MORE CHARS THAT WE CAN USE
40 TS$ = "": NW$=""
41 GET#2,S$:IF S$<>"" AND CC < 255 THEN TS$ = TS$ + S$:CC=CC+1:GOTO 41
42 NW$=MID$(TS$,176,22): TS$=""
43 FOR C = 1 TO LEN(NW$)
44 C$ = MID$(NW$,C,1)
45 IF C$="T" THEN I=C
46 NEXT
47 H$=MID$(NW$,I+1,2)
48 M$=MID$(NW$,I+4,2)
49 S$=MID$(NW$,I+7,2)
50 PRINT ""
51 PRINT H$ + ":" + M$ + ":" + S$
52 PRINT ""
53 IF VAL(H$)<25 AND VAL(M$) < 60 AND VAL(S$)<60 THEN TIME$ = H$ + M$ + S$
54 GET#2,S$:IF S$<>"" THEN GOTO 54
55 FOR W=1 TO 2000: NEXT
56 RETURN
57 PRINT CHR$(5): PRINT CHR$(147): PRINT CHR$(19);: REM clear screen, white text
58 PRINT "SELECT 1, 2 OR 3 FOR LED SEQUENCES"
59 PRINT "SELECT 4 TO SEND A MESSAGE TO IPHONE"
60 PRINT "SELECT 5 CURRENT TIME FROM THE INTERNET"
61 RETURN